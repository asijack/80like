apply plugin: 'com.android.application'
apply from: rootProject.file('script/libbase.gradle')
apply from: rootProject.file('script/releaseinfo.gradle')

android {
    def keystorePath = rootProject.ext.signConfigs.storeFile

    signingConfigs {
        release {//release版的签名配置信息
            storeFile file(keystorePath)
            storePassword rootProject.ext.signConfigs.storePassword
            keyAlias rootProject.ext.signConfigs.keyAlias
            keyPassword rootProject.ext.signConfigs.keyPassword
        }
    }

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        lintOptions {
            abortOnError false
            checkReleaseBuilds false
            disable 'MissingTranslation'
        }
    }

    buildTypes {
        release {
            minifyEnabled true //混淆编译
            shrinkResources true //移除无用资源
            zipAlignEnabled true //zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField("boolean", "LOG_DEBUG", "false")//正式版不打印日志
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            buildConfigField("boolean", "LOG_DEBUG", "true")
            applicationIdSuffix ".debug"
        }
    }

    //规定资源命名前缀为app_
    resourcePrefix "app_"

    //自定义输出apk名字
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                if (variant.getBuildType().getName() == 'release') {
                    outputFileName = new File("asijack_V" + variant.versionName + "_" +
                            new Date().format("yyyy-MM-dd_HH", TimeZone.getTimeZone("UTC")) + "_" +
                            variant.getBuildType().getName() + ".apk")
                }
            }
    }
    
    viewBinding {
        enabled = true
    }

    productFlavors {
        //基线
        base {
            keystorePath = "../keystory/base_vision"
        }
        //中性
        neutral {
            applicationId "com.neutral.forever"
            keystorePath = "../keystory/neutral_vision"
        }
        //印度
        india{
            applicationId "com.india.forever"
            keystorePath = "../keystory/india_vision"
        }
    }
}

dependencies {
    api project(':provider')
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}